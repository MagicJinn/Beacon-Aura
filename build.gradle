plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
    maven {
        url = "https://maven.isxander.dev/releases"
        name = "Xander Maven"
    }
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"beacon-aura" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
    modImplementation "dev.isxander:yet-another-config-lib:${project.yacl_version}"
    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")
}

processResources {
	inputs.property "version", project.version
	inputs.property "mod_name", project.mod_name
	inputs.property "mod_author", project.mod_author

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version,
		       "mod_name": inputs.properties.mod_name,
		       "mod_author": inputs.properties.mod_author
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
			
			pom {
				name = project.mod_name
				description = "Improves beacons by allowing you to soak in a beacon's effects with duration stacking over time."
				url = "https://github.com/MagicJinn/Beacon-Aura"
				
				developers {
					developer {
						id = "magicjinn"
						name = "MagicJinn"
						email = "magicjinn@mail.com"
					}
				}
				
				licenses {
					license {
						name = "GPL-3.0"
						url = "https://www.gnu.org/licenses/gpl-3.0"
					}
				}
				
				scm {
					connection = "scm:git:git://github.com/MagicJinn/Beacon-Aura.git"
					developerConnection = "scm:git:ssh://github.com:MagicJinn/Beacon-Aura.git"
					url = "https://github.com/MagicJinn/Beacon-Aura"
				}
			}
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}